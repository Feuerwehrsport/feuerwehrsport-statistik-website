class @FssMap
  @styleLoaded: false
  @loaded: (callback) ->
    FssMap.styleLoaded = true
    callback()

  @lat: 51
  @lon: 13

  @loadStyle: (callback) ->
    return callback() if FssMap.styleLoaded

    $.getScript '<%= asset_path('leaflet.js') %>', () ->
      L.Icon.Default.imagePath = '/styling/images/'
      $.get '<%= asset_path('leaflet.css') %>', (css) ->
        $('<style type="text/css"></style>').html(css).appendTo("head")
        FssMap.loaded(callback)

  @getMap: (id, latLon=[FssMap.lat,FssMap.lon], zoom=8) ->
    osmUrl = "https://b.tile.openstreetmap.de/tiles/osmde/{z}/{x}/{y}.png"
    osmAttrib = 'Map data Â© <a href="http://openstreetmap.org">openstreetmap</a>'
    osm = L.tileLayer(osmUrl, {attribution: osmAttrib})

    return L.map id, 
      center: latLon
      zoom: zoom
      scrollWheelZoom: false
      layers: [osm]

  @defaultIcon: () -> 
    unless FssMap.DefaultIcon?
      FssMap.DefaultIcon = L.Icon.Default.extend(options: 
        iconUrl: '<%= asset_path('map/marker-icon.png') %>'
        shadowUrl: '<%= asset_path('map/marker-shadow.png') %>'
      )
    new FssMap.DefaultIcon()

  @redIcon: () -> 
    unless FssMap.RedIcon?
      FssMap.RedIcon = L.Icon.Default.extend(options: 
        iconUrl: '<%= asset_path('map/marker-icon-red.png') %>'
        shadowUrl: '<%= asset_path('map/marker-shadow.png') %>'
      )
    new FssMap.RedIcon()
